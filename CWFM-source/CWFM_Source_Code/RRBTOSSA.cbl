       ID DIVISION.
       PROGRAM-ID.      RRBTOSSA.
C15918*AUTHOR.          SCOTT G. JENKINS / CORA AQUINO.
P27276*DATE-WRITTEN.    MAY 1987.
P27276
P27276*    CONVERT HICS FROM RAILROAD BOARD TO SOCIAL SECURITY FORMAT.
P27276
P27276*    7-BYTE RRB HIC   -  1 LETTER, 6 NUMBERS   -  X111222
P27276*    8-BYTE RRB HIC   -  2 LETTERS, 6 NUMBERS  -  XX111222
P27276*    9-BYTE RRB HIC   -  3 LETTERS, 6 NUMBERS  -  XXX111222
P27276
P27276*    10-BYTE RRB HIC  -  1 LETTER, 9 NUMBERS   -  X111222333
P27276*    11-BYTE RRB HIC  -  2 LETTERS, 9 NUMBERS  -  XX111222333
P27276*    12-BYTE RRB HIC  -  3 LETTERS, 9 NUMBERS  -  XXX111222333
P27276
P27276*    THE LETTERS ARE USED TO GENERATE THE 2-BYTE BIC IN POSITIONS
P27276*    10 AND 11 OF THE SSA HIC. IF THERE ARE 9 NUMBERS, THEY WILL
P27276*    BE PLACED IN POSITIONS 1-9, WITH THE ZONE OF THE HIGH-ORDER
P27276*    NUMBER CONVERTED FROM 'F' TO 'C' (X'F0' BECOMES X'C0', ETC.).
P27276*    IF THERE ARE 6 NUMBERS, THEY WILL BE PLACED IN POSITIONS 4-9
P27276*    WITH '{00' IN POSITIONS 1-3.
P27276
P27276*    NOTE THAT 9-DIGIT RRB HICS CANNOT HAVE '000' AS THE FIRST
P27276*    3 DIGITS, SO ANY '{00' SSA HICS MUST BE 6-DIGIT RRB HICS.
P27276
P27276*    THE FOLLOWING HIC RANGES ARE EXCLUDED FROM CONVERSION:
P27276
P27276*               RRB HIC RANGE        BIC
P27276
P27276*            A991274   - A994999      10
P27276*            XX991274  - XX994999     1X
P27276*            WCA991274 - WCA994999    13
P27276
P27276*            XX415936  - XX994999     4X
P27276*            WCD415936 - WCD994999    43
P27276
P27276*            H049160   - H994999      80
P27276*            XX049160  - XX994999     8X
P27276*            WCH049160 - WCH994999    83
P27276
P27276*    SSA-TO-RRB LOGIC DOES NOT INCLUDE THE EXCLUDED HIC RANGES,
P27276*    PRESUMABLY BECAUSE HICS IN THOSE RANGES COULD NEVER BE
P27276*    CONVERTED TO SSA FORMAT.
P27276/
P27276*****************************************************************
P27276*************** M A I N T E N A N C E   N O T E S ***************
C13542*****************************************************************
C13542*  CCR#      : 13542                PROGRAMMER: RON JESIOLOWSKI *
C13542*  MODLOG TAG: C13542               COMPANY   : EDS             *
C13542*  RELEASE NO: E930300                                          *
C13542*                                                               *
C13542* PROBLEM:  CONVERTED RRB CLAIM NUMBER EDIT IS INCORRECT.       *
C13542*                                                               *
C13542* SOLUTION: MODIFY CWF EDITS FOR RRB CONVERTED CLAIM NUMBER SO  *
C13542*           THAT IT IS CONSISTENT WITH BDMS ENROLLMENT DATABASE.*
C13542*           THE EDIT CRITERIA IS:                               *
C13542*           1) POSITION 1 MUST BE:                              *
C13542*             { (HEXIDECIMAL C0), A, B, C, D, E, F, G           *
C13542*           2) POSITION 2 THROUGH 8 MUST BE NUMERIC             *
C13542*           3) POSITIONS 1 THROUGH THREE MUST BE:               *
C13542*              A) GREATER THAN OR EQUAL TO {00 AND              *
C13542*                 LESS THAN F50                                 *
C13542*              OR                                               *
C13542*              A) GREATER THAN F95 AND LESS THAN G30            *
C13542*****************************************************************
C19775***%CCR#      : 19775                PROGRAMMER: GEETA RAMAMURTY
C19775***%MODLOG TAG: C19775               COMPANY   : CSC
C19775***%RELEASE NO: R2001200             DATE      : 01/03/2001
C19775***%
C19775***%PROBLEM   : CONVERT APPLICATION FROM COBOL II TO IBM COBOL
C19775***%
C19775***%SOLUTION  : RECOMPILE MODULE UNDER OS/390 LIBRARIES
C19775***%
C19775***%************************************************************:
C22593***%CCR#      : 22593                PROGRAMMER: MIKE ULMAN
C22593***%MODLOG TAG: C22593               COMPANY   : CSC
C22593***%RELEASE NO: R2003400             DATE      : 07/20/2003
C22593***%
C22593***%PROBLEM   : SSATORRB, RRBTOSSA, AND CABBEBIC ARE USED IN
C22593***%            ONLINE PROGRAMS BUT NOT INCLUDED IN THE ONLINE
C22593***%            LIBRARIES.
C22593***%
C22593***%SOLUTION  : CHANGE ENDEVOR TYPE TO MATCH CMNDATCV SO THE
C22593***%            THREE PROGRAMS WILL BE PRESENT IN BOTH ONLINE
C22593***%            AND BATCH LOAD LIBRARIES.
C22593***%
C22593***%************************************************************:
C25027***%CCR#      : 25027                PROGRAMMER: JOE SHANABROUGH
C25027***%MODLOG TAG: C25027               COMPANY   : CSC
C25027***%RELEASE NO: R2007100             DATE      : 08/07/2006
C25027***%
C25027***%PROBLEM   : CONVERT CWF SOFTWARE TO RUN UNDER CICS TRANS
C25027***%            SERVER 2.2
C25027***%
C25027***%SOLUTION  : RECOMPILE FOR TRANSACTION SERVER 2.2
C25027***%
C25027***%************************************************************:
C25551***%CCR#      : 25551                PROGRAMMER: JOE SHANABROUGH
C25551***%MODLOG TAG: C25551               COMPANY   : CSC
C25551***%RELEASE NO: R2008100             DATE      : 06/08/2007
C25551***%
C25551***%PROBLEM   : CONVERT CWF SOFTWARE TO RUN UNDER CICS TRANS
C25551***%            SERVER 2.3
C25551***%
C25551***%SOLUTION  : RECOMPILE FOR TRANSACTION SERVER 2.3
C25551***%
C25551***%************************************************************:
C26432***%CCR#      : 26432                PROGRAMMER: JOE SHANABROUGH
C26432***%MODLOG TAG: C26432               COMPANY   : 2020LLC
C26432***%RELEASE NO: R2009400             DATE      : 06/01/2009
C26432***%
C26432***%PROBLEM   : CONVERT CWF SOFTWARE TO RUN UNDER CICS TRANS
C26432***%            SERVER 3.2
C26432***%
C26432***%SOLUTION  : RECOMPILE FOR TRANSACTION SERVER 3.2
C26432***%
C26432***%************************************************************:
P27276***%CCR#      : 27276                PROGRAMMER: MIKE ULMAN
P27276***%MODLOG TAG: P27276               COMPANY   : 2020LLC
P27276***%RELEASE NO: R20102IP             DATE      : 03/28/2010
P27276***%
P27276***%PROBLEM   : CUMBERSOME, CONFUSING CODE.
P27276***%
P27276***%SOLUTION  : RESTRUCTURE CODE AND DOCUMENT PROCESSING.
P27276***%
P27276***%************************************************************:
C30423***%CCR#      : 30423                PROGRAMMER: S.PAIDI
C30423***%MODLOG TAG: C30423               COMPANY   : 2020LLC
C30423***%RELEASE NO: R2015300             DATE      : 02/23/2015
C30423***%
C30423***%PROBLEM   : UPGRADE CWF COBOL SOFTWARE TO V4.2
C30423***%
C30423***%SOLUTION  : RECOMPILE
C30423***%
C30423***%************************************************************:
C31663***%CCR#      : 31663                PROGRAMMER: COLBERT
C31663***%MODLOG TAG: C31663               COMPANY   : DV UNITED
C31663***%RELEASE NO: R2018100             DATE      : 09/28/2017
C31663***%
C31663***%PROBLEM   : CICS TS 5.2 SERVER UPGRADE
C31663***%
C31663***%SOLUTION  : RECOMPILE THE MODULE FOR THE CICS UPGRADE
C31663***%
C31663***%************************************************************:
C34231***%CCR#      : 34231                PROGRAMMER: L VAN VOORHIS
C34231***%MODLOG TAG: C34231               COMPANY   : DV UNITED
C34231***%RELEASE NO: C2020400             DATE      : 05/20/2020
C34231***%
C34231***%PROBLEM   : COBOL 6.2 UPGRADE
C34231***%
C34231***%SOLUTION  : RECOMPILE ONLY
C34231***%
C34231***%************************************************************:
P34569***%CCR#      : 34569                PROGRAMMER: G.COLBERT
P34569***%MODLOG TAG: P34569               COMPANY   : DV UNITED
P34569***%RELEASE NO: C20204DB             DATE      : 08/26/2020
P34569***%
P34569***%PROBLEM   : MGT DIRECTED CHANGE TO ZONEDATA AND OPTIMIZE
P34569***%            COMPILE PARAMETERS
P34569***%SOLUTION  : UPDATE PARAMETERS TO ZONEDATA(NOPFD) AND
P34569***%            OPTIMIZE(0); RECOMPILE
P34569***%************************************************************:
P27276*********** E N D   M A I N T E N A N C E   N O T E S ***********
P27276*****************************************************************
P27276/
       ENVIRONMENT DIVISION.
       DATA DIVISION.
       WORKING-STORAGE SECTION.
P27276
P27276 01  SYNC.
P27276     05                          PIC  X(32)  VALUE
P27276             'RRBTOSSA - BEGIN WORKING-STORAGE'.
P27276
P27276     05  COMP.
P27276         10  OFFSET              PIC S9(04)  VALUE ZERO.
P27276         10  HIC-LENGTH          PIC S9(04)  VALUE ZERO.
P27276
P27276     05  SSA-HIC                             VALUE SPACE.
P27276         10  SSA-HIC-BYTE-1      PIC  X(01).
P27276         10                      PIC  X(08).
P27276         10  SSA-BIC             PIC  X(02).

       01 WS-COUNT PIC 9(3) VALUE 0.
P27276
P27276 LINKAGE SECTION.
P27276
P27276 01  RRBTOSSA-PARAMETERS.
P27276     05  RP-RRB-HIC              PIC  X(12).
P27276     05  RP-SSA-HIC              PIC  X(11).
P27276/
P27276 PROCEDURE DIVISION           USING  RRBTOSSA-PARAMETERS.
P27276
P27276*    INSPECT...TALLYING GENERATES A CALL TO A SUBROUTINE,
P27276*    SO PERFORM...VARYING USES A FRACTION OF THE CPU-TIME.
P27276
P27276     MOVE  SPACE                 TO  SSA-HIC
P27276                                     RP-SSA-HIC
P27276     PERFORM
P27276       VARYING HIC-LENGTH      FROM  +12
P27276                                 BY  -1
P27276       UNTIL   HIC-LENGTH         <  +1
P27276       OR      RP-RRB-HIC (HIC-LENGTH:1)
P27276                              NOT =  SPACE
               ADD 1 TO WS-COUNT GIVING WS-COUNT
P27276     END-PERFORM
P27276
P27276*    7-BYTE RRB HIC   -  1 LETTER, 6 NUMBERS   -  X111222
P27276*    8-BYTE RRB HIC   -  2 LETTERS, 6 NUMBERS  -  XX111222
P27276*    9-BYTE RRB HIC   -  3 LETTERS, 6 NUMBERS  -  XXX111222
P27276
P27276*    10-BYTE RRB HIC  -  1 LETTER, 9 NUMBERS   -  X111222333
P27276*    11-BYTE RRB HIC  -  2 LETTERS, 9 NUMBERS  -  XX111222333
P27276*    12-BYTE RRB HIC  -  3 LETTERS, 9 NUMBERS  -  XXX111222333
P27276
P27276*    THE LETTERS ARE USED TO GENERATE THE 2-BYTE BIC IN POSITIONS
P27276*    10 AND 11 OF THE SSA HIC. IF THERE ARE 9 NUMBERS, THEY WILL
P27276*    BE PLACED IN POSITIONS 1-9, WITH THE ZONE OF THE HIGH-ORDER
P27276*    NUMBER CONVERTED FROM 'F' TO 'C' (X'F0' BECOMES X'C0', ETC.).
P27276*    IF THERE ARE 6 NUMBERS, THEY WILL BE PLACED IN POSITIONS 4-9
P27276*    WITH '{00' IN POSITIONS 1-3.
P27276
P27276     EVALUATE  HIC-LENGTH
P27276       WHEN  +7
P27276       WHEN  +10
P27276         MOVE  +2                TO  OFFSET
P27276
P27276         EVALUATE  TRUE
P27276           WHEN  RP-RRB-HIC (1:1) =  'A'
P27276             MOVE  '10'          TO  SSA-BIC
P27276
P27276           WHEN  RP-RRB-HIC (1:1) =  'H'
P27276           AND   HIC-LENGTH       =  +7
P27276             MOVE  '80'          TO  SSA-BIC
P27276         END-EVALUATE
P27276
P27276       WHEN  +8
P27276       WHEN  +11
P27276         MOVE  +3                TO  OFFSET
P27276
P27276         EVALUATE  TRUE
P27276           WHEN  RP-RRB-HIC (1:2) =  'MA'
P27276             MOVE  '14'          TO  SSA-BIC
P27276
P27276           WHEN  RP-RRB-HIC (1:2) =  'PA'
P27276             MOVE  '15'          TO  SSA-BIC
P27276
P27276           WHEN  RP-RRB-HIC (1:2) =  'WA'
P27276             MOVE  '16'          TO  SSA-BIC
P27276
P27276           WHEN  RP-RRB-HIC (1:2) =  'CA'
P27276             MOVE  '17'          TO  SSA-BIC
P27276
P27276           WHEN  RP-RRB-HIC (1:2) =  'PD'
P27276             MOVE  '45'          TO  SSA-BIC
P27276
P27276           WHEN  RP-RRB-HIC (1:2) =  'WD'
P27276             MOVE  '46'          TO  SSA-BIC
P27276
P27276           WHEN  HIC-LENGTH       =  +11
P27276             CONTINUE
P27276
P27276           WHEN  RP-RRB-HIC (1:2) =  'JA'
P27276             MOVE  '11'          TO  SSA-BIC
P27276
P27276           WHEN  RP-RRB-HIC (1:2) =  'MH'
P27276             MOVE  '84'          TO  SSA-BIC
P27276
P27276           WHEN  RP-RRB-HIC (1:2) =  'PH'
P27276             MOVE  '85'          TO  SSA-BIC
P27276
P27276           WHEN  RP-RRB-HIC (1:2) =  'WH'
P27276             MOVE  '86'          TO  SSA-BIC
P27276         END-EVALUATE
P27276
P27276       WHEN  +9
P27276       WHEN  +12
P27276         MOVE  +4                TO  OFFSET
P27276
P27276         EVALUATE  TRUE
P27276           WHEN  RP-RRB-HIC (1:3) =  'WCA'
P27276             MOVE  '13'          TO  SSA-BIC
P27276
P27276           WHEN  RP-RRB-HIC (1:3) =  'WCD'
P27276             MOVE  '43'          TO  SSA-BIC
P27276
P27276           WHEN  RP-RRB-HIC (1:3) =  'WCH'
P27276           AND   HIC-LENGTH       =  +9
P27276             MOVE  '83'          TO  SSA-BIC
P27276         END-EVALUATE
P27276     END-EVALUATE
P27276
P27276     EVALUATE  TRUE
P27276       WHEN  SSA-BIC              =  SPACE
P27276         CONTINUE
P27276
P27276       WHEN  HIC-LENGTH          <=  +9
P27276         MOVE  '{00'             TO  SSA-HIC (1:3)
P27276         MOVE  RP-RRB-HIC (OFFSET:6)
P27276                                 TO  SSA-HIC (4:6)
P27276
P27276         IF  (SSA-HIC (4:6)           NUMERIC
P27276         AND SSA-HIC (4:6)    NOT =  ZERO)
P27276
P27276*        FOR RRB HICS WITH SIX NUMERIC DIGITS, EXCLUDE INVALID
P27276*        COMBINATIONS OF BICS AND SIX-DIGIT NUMERIC RANGES.
P27276
P27276         AND ((SSA-BIC (1:1)      =  '1'
P27276         AND  (SSA-HIC (4:6)      <  '991274'
P27276         OR                       >  '994999'))
P27276
P27276         OR   (SSA-BIC (1:1)      =  '4'
P27276         AND  (SSA-HIC (4:6)      <  '415936'
P27276         OR                       >  '994999'))
P27276
P27276         OR   (SSA-BIC (1:1)      =  '8'
P27276         AND  (SSA-HIC (4:6)      <  '049160'
P27276         OR                       >  '994999')))
P27276             MOVE  SSA-HIC       TO  RP-SSA-HIC
P27276         END-IF
P27276
P27276       WHEN  OTHER
P27276         MOVE  RP-RRB-HIC (OFFSET:9)
P27276                                 TO  SSA-HIC (1:9)
P27276
P27276*        'INSPECT' GENERATES A SINGLE ASSEMBLER 'TR' INSTRUCTION
P27276*        PROCESSING AN UNQUALIFIED FIELD, BUT GENERATES A LINK TO
P27276*        A SUBROUTINE IF REFERENCE MODIFICATION IS USED.
P27276
P27276         IF  SSA-HIC (1:9)           NUMERIC
P27276         AND SSA-HIC (1:9)    NOT =  ALL '9'
P27276         AND SSA-HIC (1:3)    NOT =  ZERO
P27276             INSPECT SSA-HIC-BYTE-1
P27276                         CONVERTING  '0123456789'
P27276                                 TO  '{ABCDEFGHI'
P27276             MOVE  SSA-HIC       TO  RP-SSA-HIC
P27276         END-IF
P27276     END-EVALUATE
P27276
P27276     GOBACK.